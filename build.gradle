apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" } // To fetch from Github
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile 'com.github.peq:jMpq:e9ddcaa0cf2a3d860d2bc59779690312bbbb3c20'
    compile 'org.antlr:antlr4:4.5'
    compile 'junit:junit:4.11'
    compile 'com.google.guava:guava:20.0'
    compile group: 'com.eclipsesource.minimal-json', name: 'minimal-json', version: '0.9.4'
}

jar {
    baseName = 'vrjc'
    manifest {
        attributes 'Main-Class': 'ruke.vrj.Main'
    }
    // Include dependencies in final jar
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

compileJava {
    sourceCompatibility = '1.8'
}

checkstyle {
    toolVersion '7.5'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled false
    }
}

task buildGrammar (type:Exec, description: 'Build ANTLR grammar') {
    commandLine 'java', '-jar', 'bin/antlr.jar', '-o', 'src/main/java/ruke/vrj/antlr', '-package', 'ruke.vrj.antlr', '-Dlanguage=Java', '-encoding', 'utf-8', '-visitor', '-no-listener', '-lib', '.', 'vrj.g4'
}

compileJava.dependsOn buildGrammar